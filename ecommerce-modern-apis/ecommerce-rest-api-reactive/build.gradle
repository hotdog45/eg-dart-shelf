plugins {
	id 'org.springframework.boot' version '2.7.2'
	id 'io.spring.dependency-management' version '1.0.12.RELEASE'
	id 'java'
	id 'org.hidetake.swagger.generator' version '2.19.2'
}

group = 'com.gusrylmubarok.ecommerce'
version = '1.0.0'
sourceCompatibility = '17'

swaggerSources {
	def typeMappings = 'URI=URI, BigDecimal=BigDecimal, EntityModel=EntityModel'
	def importMappings = 'URI=java.net.URI, BigDecimal=java.math.BigDecimal, EntityModel=org.springframework.hateoas.EntityModel'
	eCommerce {
		def apiYaml = "${rootDir}/src/main/resources/api/openapi.yaml"
		def configJson = "${rootDir}/src/main/resources/api/config.json"
		inputFile = file(apiYaml)
		def ignoreFile = file("${rootDir}/src/main/resources/api/.openapi-generator-ignore")
		code {
			language = 'spring'
			configFile = file(configJson)
			rawOptions = ['--ignore-file-override', ignoreFile, '--type-mappings',
						  typeMappings, '--import-mappings', importMappings] as List<String>
			components = [models: true, apis: true, supportingFiles: 'ApiUtil.java']
			//dependsOn validation // Should be uncommented once plugin starts supporting OA 3 validation
		}
	}
}

compileJava.dependsOn swaggerSources.eCommerce.code
sourceSets.main.java.srcDir "${swaggerSources.eCommerce.code.outputDir}/src/main/java"
sourceSets.main.resources.srcDir "${swaggerSources.eCommerce.code.outputDir}/src/main/resources"

repositories {
	mavenCentral()
}

dependencies {
	// OpenAPI Starts
	swaggerCodegen 'org.openapitools:openapi-generator-cli:6.0.1'
	swaggerUI 'org.webjars:swagger-ui:4.11.1'
	compileOnly 'io.swagger:swagger-annotations:1.6.6'
	compileOnly group: 'io.swagger.core.v3', name: 'swagger-annotations', version: '2.2.2'
	compileOnly 'org.springframework.boot:spring-boot-starter-validation'
	compileOnly 'org.openapitools:jackson-databind-nullable:0.2.3'
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml'
	implementation group: 'javax.xml.bind', name: 'jaxb-api', version: '2.3.1'
	implementation 'org.springframework.boot:spring-boot-starter-hateoas'
	// OpenAPI Ends

	implementation 'org.springframework.boot:spring-boot-starter-webflux'

	// DB Starts
	implementation 'org.springframework.boot:spring-boot-starter-data-r2dbc'
	implementation 'com.h2database:h2'
	runtimeOnly 'io.r2dbc:r2dbc-h2'
	// DB Ends

	// Flyway: Start
	implementation 'org.flywaydb:flyway-core'
	implementation 'org.springframework.boot:spring-boot-starter-jdbc'
	// Flyway: End

	testImplementation('org.springframework.boot:spring-boot-starter-test')
	testImplementation 'io.projectreactor:reactor-test'
}

configurations.all {
	resolutionStrategy.dependencySubstitution.all { dependency ->
		if (dependency.requested instanceof ModuleComponentSelector && dependency.requested.module == 'spring-boot-starter-reactor-netty') {
			dependency.useTarget("org.springframework.boot:spring-boot-starter-tomcat:$dependency.requested.version", 'Use Undertow instead of Reactor Netty')
		}
	}
}

tasks.named('test') {
	useJUnitPlatform()
}
